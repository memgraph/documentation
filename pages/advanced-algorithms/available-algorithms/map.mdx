---
title: map
description: Explore Memgraph's map utility for efficient graph data mapping and navigation. Make use of tutorials and comprehensive documentation to learn how to effectively utilize the map utility in Memgraph.
---

import { Callout } from 'nextra/components'

# map



The `map` module offers a versatile toolkit for manipulating collections of key-value pairs, enabling advanced data operations within a graph database context.

[![docs-source](https://img.shields.io/badge/source-map-FB6E00?logo=github&style=for-the-badge)](https://github.com/memgraph/mage/tree/main/cpp/map_module)

| Trait               | Value                                                 |
| ------------------- | ----------------------------------------------------- |
| **Module type**     | util  |
| **Implementation**  | C++        |
| **Graph direction** | directed/undirected |
| **Edge weights**    | weighted/unweighted |
| **Parallelism**     | sequential |

## Functions

### `remove_key(map, key, config)`

Removes the specified key and its corresponding value from the input map. If the key does not exist in the input map, it will be ignored. Additionally, when the recursive config is enabled, the key will also be removed from any inner maps that are part of the input map.

{<h4> Input: </h4>}

- `map: Map` ➡ the map from which the key will be removed.
- `key: string` ➡ the key to be removed from the map.
- `config: Map default = {}` ➡ config map which supports the `recursive` option. `recursive` is `false` by default, 
and should be set to `true` if the input map consists of values that are also maps and, therefore, may have the key to be removed. Example of `config` map: 
`{recursive: true}`. If anything other than `false` or `true` is placed as a value for `recursive`, it is as if `false` was placed. 

{<h4> Output: </h4>}

- `Map` ➡ the map after removing the specified key.

{<h4> Usage: </h4>}

```cypher
RETURN map.remove_key({c: "b", d: "ba"}, "c") AS removed;
```

```plaintext
+----------------------------+
| removed                    |
+----------------------------+
| {"d": "ba"}                |
+----------------------------+
```

Example with recursion:

```cypher
RETURN map.remove_key({c: "b", d: {e: "ba", c: "h", a: {c: "z"}}}, "c",{recursive: true}) AS removed;
```

```plaintext
+----------------------------+
| removed                    |
+----------------------------+
| {"d": {"e": "ba"}}         |
+----------------------------+
```


### `from_pairs(pairs)`

Creates a map from a list of pairs, where each pair is essentially another list of size 2. The first element in each pair must be of type `string`, as it will be used as a key in the resulting map.

{<h4> Input: </h4>}

- `pairs: List[List]` ➡ list of pairs.


{<h4> Output: </h4>}

- `Map` ➡ a map whose keys are the first elements in pairs, and the corresponding values are the second elements in the pairs.

{<h4> Usage: </h4>}

```cypher
RETURN map.from_pairs([["b", 3], ["c", "c"]]) AS map;
```

```plaintext
+----------------------------+
| map                        |
+----------------------------+
| {"b": 3, "c": "c"}         |
+----------------------------+
```


### `merge(first, second)`

Merges two maps into one. If the same key occurs twice, the later value will overwrite the previous one.

{<h4> Input: </h4>}

- `first: Map` ➡ a map containing key-value pairs that need to be merged with others.
- `second: Map` ➡ another map containing key-value pairs that need to be merged with others.


{<h4> Output: </h4>}

- `Map` ➡ merged input maps.

{<h4> Usage: </h4>}

```cypher
RETURN map.merge({a: "b", c: "d"}, {e: "f", g: "h"}) AS merged;
```

```plaintext
+----------------------------------------+
| merged                                 |
+----------------------------------------+
| {a: "b", c: "d", e: "f", g: "h"}       |
+----------------------------------------+
```


### `flatten(map, delimiter)`

Flattens nested items in the input map.

{<h4> Input: </h4>}

- `map: Map[Any]` ➡ the map used in `flatten`.
- `delimiter: string (default = ".")` ➡ the delimiter used for flattening.


{<h4> Output: </h4>}

- `Map[Any]` ➡ flattened map, sorted alphabetically by keys.

{<h4> Usage: </h4>}

```cypher
RETURN map.flatten({a: {b:3, d:4}},"/") AS result;
```

```plaintext
+----------------------------------------+
| result                                 |
+----------------------------------------+
| {"a/b": 3, "a/d": 4}                   |
+----------------------------------------+
```

### `from_lists(keys, values)`

Makes a map from lists of keys and corresponding values.

{<h4> Input: </h4>}

- `keys: List[string]` ➡ the list of keys.
- `values` ➡ the list of values.


{<h4> Output: </h4>}

- `Map[Any]` ➡ the resulting map.

{<h4> Usage: </h4>}

```cypher
RETURN map.from_lists(["key","key2"],[1,2]) AS result;
```

```plaintext
+----------------------------------------+
| result                                 |
+----------------------------------------+
| {""key": 1, "key2": 2}                 |
+----------------------------------------+
```
### `remove_keys(map, keys, config)`

Removes keys from input map. If recursive option is true, will remove keys from maps nested inside the map. 

{<h4> Input: </h4>}

- `map: Map[Any]` ➡ the input map.
- `keys: List[string]` ➡ the list of keys that will be removed.
- `config: Map default = {}` ➡ config map which supports the `recursive` option. `recursive` is `false` by default, 
and should be set to `true` if the input map consists of values that are also maps and, therefore, may have the key to be removed. Example of `config` map: 
`{recursive: true}`. If anything other than `false` or `true` is placed as a value for `recursive`, it is as if `false` was placed. 


{<h4> Output: </h4>}

- `Map[Any]` ➡ the resulting map.

{<h4> Usage: </h4>}
Usage when `recursive` is `false`:

```cypher
RETURN map.remove_keys({key: 1, key2:{key : 3, key3: 5}},["key"],{recursive: false}) AS result;
```

```plaintext
+----------------------------------------+
| result                                 |
+----------------------------------------+
|{"key2": {"key": 3,"key3": 5}}          |
+----------------------------------------+
```


Usage with recursive = true:

```cypher
RETURN map.remove_keys({key: 1, key2:{key : 3, key3: 5}},["key"],{recursive: true}) AS result;
```

```plaintext
+----------------------------------------+
| result                                 |
+----------------------------------------+
|{"key2": {"key3": 5}}                   |
+----------------------------------------+
```


### `from_values(values)`

Returns a map from the given list of values. The list has the format: `[key1, value1, key2, value2]`. If the key is not convertible to a string, throws `ValueException`.

#### Input:

- `values: List[Any]` ➡ list of values.

#### Output:

- `Map` ➡ the resulting map.

#### Usage:

```cypher
RETURN map.from_values(["day", "sunny", 5, 6]) AS map;
```

```plaintext
+----------------------------------------+
|        map                             |
+----------------------------------------+
| {"5": 6, "day": "sunny"}               |
+----------------------------------------+
```

### `set_key(map, key, value)`

Updates the value at the position `key` in `map`. If the key doesn't exist, inserts it.

#### Input:

- `map: Map` ➡ map to be modified.
- `key: string` ➡ the position in `map` to be updated.
- `value: any` ➡ new value at the position `key` in `map`.

#### Output:

- `Map` ➡ the modified map.

#### Usage:

```cypher
RETURN map.set_key({name:"Ivan",country:"Croatia"}, "name", "Matija") AS map;
```

```plaintext
+-------------------------------------------+
|                     map                   |
+-------------------------------------------+
| {"country": "Croatia", "name": "Matija"}  |
+-------------------------------------------+
```

## Procedures

### `from_nodes(label, property)`

Returns a map of all nodes which contain the given label and property. The key of each map element will be the value of the property (if it is convertible to a string, otherwise throws `ValueException`).

{<h4> Input: </h4>}

- `label: string` ➡ the wanted label.
- `property: string` ➡ the wanted property.

{<h4> Output: </h4>}

- `result: Map` ➡ the resulting map.

{<h4> Usage: </h4>}
```cypher
CREATE (Oppenheimer:Movie {title:'Oppenheimer', released:2023});
CREATE (Barbie:Movie {title:"Barbie", released:2023});
CREATE (Shawshank:Movie {title:'The Shawshank Redemption', released:1994});

CALL map.from_nodes("Movie", "title") YIELD map RETURN map;
```

```plaintext
{
   "Barbie": {
      "identity": 53,
      "labels": [
         "Movie"
      ],
      "properties": {
         "released": 2023,
         "title": "Barbie"
      }
   },
   "Oppenheimer": {
      "identity": 52,
      "labels": [
         "Movie"
      ],
      "properties": {
         "released": 2023,
         "title": "Oppenheimer"
      }
   },
   "The Shawshank Redemption": {
      "identity": 54,
      "labels": [
         "Movie"
      ],
      "properties": {
         "released": 1994,
         "title": "The Shawshank Redemption"
      }
   }
}
```