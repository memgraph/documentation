---
title: node
description: Get a comprehensive toolkit for managing graph nodes, enabling creation, deletion, updating, merging, and more.
---

import { Callout } from 'nextra/components'

# node



The `node` module provides a comprehensive toolkit for managing graph nodes, enabling creation, deletion, updating, merging, and more.

[![docs-source](https://img.shields.io/badge/source-node-FB6E00?logo=github&style=for-the-badge)](https://github.com/memgraph/mage/tree/main/cpp/node_module)

| Trait               | Value                                                 |
| ------------------- | ----------------------------------------------------- |
| **Module type**     | util  |
| **Implementation**  | C++        |
| **Graph direction** | directed/undirected |
| **Edge weights**    | weighted/unweighted |
| **Parallelism**     | sequential |

## Procedures

<Callout type="info">
You can execute this algorithm on [graph projections, subgraphs or portions of the graph](/advanced-algorithms/run-algorithms#run-procedures-on-subgraph).
</Callout>

### `relationship_types()`

Returns a list of distinct relationship types of the given node contained within the given list of types. If the list of types is empty returns all distinct relationship types. Relationship types can also be directed:
- &lt;type - incoming relationship.
- type> - outgoing relationship.
- type - either way.

{<h4> Input: </h4>}

- `node: Node` ➡ the given node.
- `types: List[string] (default = [])` ➡ list of relationship types to filter by.

{<h4> Output: </h4>}

- `relationship_types: List[string]` ➡ list of distinct relationship types contained within the given list of types.

{<h4> Usage: </h4>}

```cypher
CREATE (ivan: Intern {name: 'Ivan'})
CREATE (idora: Intern {name: 'Idora'})
CREATE (matija: Intern {name: 'Matija'})
MERGE (ivan)-[:KNOWS]->(idora)
MERGE (matija)-[:HEARS]->(idora)
MERGE (matija)-[:SEES]->(ivan);
```

```cypher
MATCH (intern:Intern) CALL node.relationship_types(intern, ["<KNOWS", "SEES>", "HEARS"]) yield relationship_types return intern.name as name, relationship_types;
```

```plaintext
+--------------------------------+
| name      relationship_types   |
+--------------------------------+
| Ivan      []                   |
+--------------------------------+
| Idora     ["HEARS", "KNOWS"]   |
+--------------------------------+
| Matija    ["SEES", "HEARS"]    |
+--------------------------------+
```

### `relationship_exists()`

Checks if given node has a relationship of the pattern.  

{<h4> Input: </h4>}

- `node: Node` ➡ node for which relationship existance is to be verified.
- `pattern: List[string] (optional)` ➡ list of relationship types for which it will be checked if at least one of them exists; if nothing is stated, procedure checks all types of relationships.

If &lt; is added in front of the relationship type, only relationships coming into the node will be checked (e.g., "&lt;KNOWS"), while if > is added at the end of the relationship type, only relationships coming from the node will be checked (e.g., "KNOWS>").
Furthermore, if relationship type is not relevant, it is possible to enter only &lt; or > to check incoming or outgoing relationships.

{<h4> Output: </h4>}

- `exists: bool` ➡ whether or not provided node has a relationship of specified type.

{<h4> Usage: </h4>}

```cypher
MERGE (a:Person {name: "Phoebe"}) MERGE (b:Person {name: "Joey"}) CREATE (a)-[f:FRIENDS]->(b);
MATCH (a:Person {name: "Joey"}) CALL node.relationship_exists(a, ["<FRIENDS"]) 
YIELD exists RETURN exists;
```

```plaintext
+----------------------------+
| exists                     |
+----------------------------+
| True                       |
+----------------------------+
```

### `relationships_exist()`

Checks if relationships in the input list exist at the given node. Results are returned as a map, represented as string-bool pair, where string represents the relationship, and bool represents if the relationship exists or not. Relationships can be directed, and the syntax for direction specification is provided below:
- &lt;type - incoming relationship.
- type> - outgoing relationship.
- type - both incoming and outgoing.
- anything else results in an exception.

{<h4> Input: </h4>}

- `node: Node` ➡ the input node.
- `relationships: List[string]` ➡ list of relationships to be checked.

{<h4> Output: </h4>}

- `result: Map` ➡ map of string-bool pairs, where string represents the relationship, and bool represents if the relationship exist or not. Example: `{rel1: true, rel2>: false}` means rel1 exists, and outgoing rel2 doesn't exist.

{<h4> Usage: </h4>}

```cypher
CREATE (d:Dog)-[l:LOVES]->(h:Human)-[t:TAKES_CARE_OF]->(d);
```

```cypher
MATCH (d:Dog) CALL node.relationships_exist(d, ["LOVES>", "TAKES_CARE_OF", "FOLLOWS", "<LOVES"]) YIELD result RETURN result;
```

```plaintext
+----------------------------------------------------------------------------+
| result                                                                     |
+----------------------------------------------------------------------------+
| {"<LOVES": false, "FOLLOWS": false, "LOVES>": true, "TAKES_CARE_OF": true} |                  
+----------------------------------------------------------------------------+
```

```cypher
MATCH (h:Human) CALL node.relationships_exist(h, ["LOVES>", "TAKES_CARE_OF", "FOLLOWS", "<LOVES"]) YIELD result RETURN result;
```

```plaintext
+----------------------------------------------------------------------------+
| result                                                                     |
+----------------------------------------------------------------------------+
| {"<LOVES": true, "FOLLOWS": false, "LOVES>": false, "TAKES_CARE_OF": true} |                  
+----------------------------------------------------------------------------+
```


## Functions

### `degree_in()`

Returns the in degree of the input node. If input `type` is specified, only relationships with the type specified will be counted for node degree.

{<h4> Input: </h4>}

- `node: Node` ➡ the input node.
- `type: string, default = ""` ➡ optional relationship type, which will be counted for degree calculation.

{<h4> Output: </h4>}

An integer that represents the node in degree value.

{<h4> Usage: </h4>}

```cypher
CREATE (d:Dog),(h:Human),(d)-[l:LOVES]->(h),(d)-[o:OWNED_BY]->(h);
```

```cypher
MATCH (h:Human) RETURN node.degree_in(h) AS result;
```

```plaintext
+----------------------------------------------------------------------------+
| result                                                                     |
+----------------------------------------------------------------------------+
| 2                                                                          |                  
+----------------------------------------------------------------------------+
```

```cypher
MATCH (h:Human) RETURN node.degree_in(h, "LOVES") AS result;
```

```plaintext
+----------------------------------------------------------------------------+
| result                                                                     |
+----------------------------------------------------------------------------+
| 1                                                                          |                  
+----------------------------------------------------------------------------+
```

### `degree_out()`

Returns the out degree of the input node. If input `type` is specified, only relationships with the type specified will be counted for node degree.

{<h4> Input: </h4>}

- `node: Node` ➡ the input node.
- `type: string, default = ""` ➡ optional relationship type, which will be counted for degree calculation.

{<h4> Output: </h4>}

An integer that represents the node out degree value.

{<h4> Usage: </h4>}

```cypher
CREATE (d:Dog),(h:Human),(d)-[l:LOVES]->(h),(d)-[o:OWNED_BY]->(h);
```

```cypher
MATCH (d:Dog) RETURN node.degree_out(d) AS result;
```

```plaintext
+----------------------------------------------------------------------------+
| result                                                                     |
+----------------------------------------------------------------------------+
| 2                                                                          |                  
+----------------------------------------------------------------------------+
```

```cypher
MATCH (d:Dog) RETURN node.degree_out(d, "LOVES") AS result;
```

```plaintext
+----------------------------------------------------------------------------+
| result                                                                     |
+----------------------------------------------------------------------------+
| 1                                                                          |                  
+----------------------------------------------------------------------------+
```