---
title: Best practices
description: Learn best practices for multi-tenancy in Memgraph, including default database recommendations, user privileges, and audit logs.
---

import { Callout } from 'nextra/components'

# Best practices

## Default database best practices

In multi-tenant environments, we recommend treating the default "memgraph"
database as an administrative/system database rather than storing application
data in it. This approach provides better security and isolation, especially
given recent changes to authentication and authorization requirements.

### Why treat memgraph as an admin database?

As of Memgraph v3.5, users have to have both the `AUTH` privilege and access to
the default "memgraph" database to execute authentication and authorization
queries. Additionally, replication queries (such as `REGISTER REPLICA`, `SHOW
REPLICAS`, etc.) and multi-database queries (such as `SHOW DATABASES`, `CREATE
DATABASE`, etc.) also now target the "memgraph" database and require access to
it. This requirement affects multi-tenant environments where users might have
access to other databases but not the default one.

### Recommended setup

1. **Restrict memgraph database access**: Only grant access to the "memgraph"
   database to privileged users who need to perform system administration tasks
2. **Use tenant-specific databases**: Store all application data in dedicated
   tenant databases
3. **Separate concerns**: Keep user management, role management, system
   administration, replication management, and multi-database management
   separate from application data

### Example configuration

```cypher
-- Create admin role with full system privileges
CREATE ROLE system_admin;
GRANT ALL PRIVILEGES TO system_admin;
GRANT DATABASE memgraph TO system_admin;

-- Create tenant-specific roles (no access to memgraph database)
CREATE ROLE tenant1_admin;
CREATE ROLE tenant1_user;
CREATE ROLE tenant2_admin;
CREATE ROLE tenant2_user;

-- Grant appropriate permissions to tenant roles
GRANT MATCH, CREATE, MERGE, SET, DELETE, INDEX TO tenant1_admin;
GRANT CREATE, READ, UPDATE, DELETE ON NODES CONTAINING LABELS * TO tenant1_admin;
GRANT CREATE, READ, UPDATE, DELETE ON EDGES CONTAINING TYPES * TO tenant1_admin;
GRANT MATCH, CREATE, MERGE, SET, DELETE TO tenant1_user;
GRANT CREATE, READ, UPDATE, DELETE ON NODES CONTAINING LABELS * TO tenant1_user;
GRANT CREATE, READ, UPDATE, DELETE ON EDGES CONTAINING TYPES * TO tenant1_user;
GRANT MATCH, CREATE, MERGE, SET, DELETE, INDEX TO tenant2_admin;
GRANT CREATE, READ, UPDATE, DELETE ON NODES CONTAINING LABELS * TO tenant2_admin;
GRANT CREATE, READ, UPDATE, DELETE ON EDGES CONTAINING TYPES * TO tenant2_admin;
GRANT MATCH, CREATE, MERGE, SET, DELETE TO tenant2_user;
GRANT CREATE, READ, UPDATE, DELETE ON NODES CONTAINING LABELS * TO tenant2_user;
GRANT CREATE, READ, UPDATE, DELETE ON EDGES CONTAINING TYPES * TO tenant2_user;

-- Grant access only to tenant databases
GRANT DATABASE tenant1_db TO tenant1_admin, tenant1_user;
GRANT DATABASE tenant2_db TO tenant2_admin, tenant2_user;

-- Create users
CREATE USER system_admin_user IDENTIFIED BY 'admin_password';
CREATE USER tenant1_admin_user IDENTIFIED BY 't1_admin_pass';
CREATE USER tenant1_regular_user IDENTIFIED BY 't1_user_pass';
CREATE USER tenant2_admin_user IDENTIFIED BY 't2_admin_pass';
CREATE USER tenant2_regular_user IDENTIFIED BY 't2_user_pass';

-- Assign roles
SET ROLE FOR system_admin_user TO system_admin;
SET ROLE FOR tenant1_admin_user TO tenant1_admin;
SET ROLE FOR tenant1_regular_user TO tenant1_user;
SET ROLE FOR tenant2_admin_user TO tenant2_admin;
SET ROLE FOR tenant2_regular_user TO tenant2_user;
```

In this configuration:
- `system_admin_user` can perform all authentication/authorization, replication,
  and multi-database operations and has access to the "memgraph" database
- Tenant users can only access their respective tenant databases
- Application data is completely isolated in tenant-specific databases
- The "memgraph" database serves purely as an administrative database

## User privileges and database access

Authentication and authorization data are shared across databases, providing a
unified source of truth. A single user can access multiple databases with a
global set of privileges, but currently, per-database privileges cannot be
granted.

<Callout type="warning">

User-role mappings are simple maps located in the user. Deleting or renaming the database will not update this information. The admin needs to make sure the correct access is maintained at all times.

</Callout>

Access to all databases can be granted or revoked using wildcards:
`GRANT DATABASE * TO user;`, `DENY DATABASE * FROM user;` or 
`REVOKE DATABASE * FROM user;`.

## Impact on multi-tenant environments

In multi-tenant environments where users might not have access to the "memgraph"
database, multi-database management operations will fail. This reinforces the
recommendation to treat the "memgraph" database as an administrative/system
database.

### Best practice

For multi-database management, ensure that users who need to perform
multi-database operations have both the appropriate multi-database privileges
and access to the "memgraph" database. This aligns with the overall
recommendation to treat the "memgraph" database as an administrative database in
multi-tenant environments.

## Example: Admin user with multi-database privileges

```cypher
-- Create admin role with multi-database privileges
CREATE ROLE multi_db_admin;
GRANT MULTI_DATABASE_USE, MULTI_DATABASE_EDIT TO multi_db_admin;
GRANT DATABASE memgraph TO multi_db_admin;

-- Create user with multi-database admin role
CREATE USER db_admin IDENTIFIED BY 'admin_password';
SET ROLE FOR db_admin TO multi_db_admin;
```

In this setup, `db_admin` can:
- Execute all multi-database queries (`SHOW DATABASES`, `CREATE DATABASE`, `DROP
  DATABASE`, `RENAME DATABASE`, etc.)
- Access the "memgraph" database for administrative operations
- Manage the multi-tenant database configuration

## Audit Logs

Audit logs now encompass the active database name, positioned immediately after
the username field.

