---
title: Model a graph from CSV file
description: Learn how to model your existing data using dummy data before importing your real data into Memgraph.
---

import { Cards } from 'nextra/components'

# Model a graph from CSV file

This guide will teach you how to model your existing data using dummy
data before importing your real data into Memgraph. By following the guide,
you‚Äôll learn to create efficient, clean, and optimized graph models to maximize
performance.

<h3 className="custom-header">Who is this guide for?</h3>

This guide is designed for:

- Beginners or less experienced graph database users.
- Anyone unsure of how to model data or import it into Memgraph for maximum
  speed and efficiency.

<h3 className="custom-header">What you‚Äôll need for this?</h3>

- Memgraph Platform installed. Quick start: for Linux/macOS run `curl
  https://install.memgraph.com | sh` and for Windows run `iwr
  https://windows.memgraph.com | iex`. Read detailed instructions
  in the [getting started guide](https://memgraph.com/docs/getting-started).
- A¬†CSV file¬†containing your data [stored in the running Memgraph container](/getting-started/first-steps-with-docker#copy-files-from-and-to-a-docker-container). For this guide, we provide a [dummy file with
  random
  data](https://public-assets.memgraph.com/import-data/data-modeling/random_data.csv).
- Basic knowledge of databases (helpful but not mandatory).

## Introduction to graph data modeling

**Data modeling**¬†is key for the best performance and efficient use of storage
in any database. To get the most out of a database, you need to take advantage
of its specific strengths. In the case of graph databases, you use the graph
structure‚Äînodes and relationships‚Äîso that you can avoid duplicating data
unnecessarily.

Think of nodes as¬†**entities**¬†in a relational database and relationships
as¬†foreign keys. However, graph databases offer a key advantage: you can store
information directly on nodes, without repeating it in relationships.

For example, let's say you have a data on orders and workers. In a relational
database, you‚Äôd need to duplicate some of a worker‚Äôs details (e.g., name and
position) when connecting them to an order. But in a graph database:

- Worker details are stored on the¬†`Worker` node.
- The relationship to the¬†`Order` node¬†only stores the order number.

By keeping your data model clean and efficient, you reduce storage costs, and
lower the total cost of ownership. 

<h3 className="custom-header">Overview of the dummy CSV file</h3>

This guide is designed to give you hands-on examples of how to model data in a
graph database. We‚Äôll be working with data that represents one million randomly
generated individuals, each with properties like name, surname, age, and date of
birth. The data will be formatted in a CSV file, with each row representing one
individual. Here is how the header and the first couple of rows look like:

| Name     | Surname   | Age | Date_of_Birth |
|----------|----------|-----|--------------|
| Justin   | Diaz     | 18  | 2005-10-23   |
| Donald   | Jones    | 35  | 1989-02-28   |
| David    | Harvey   | 71  | 1953-04-29   |
| Timothy  | Morris   | 49  | 1975-06-05   |
| Gabriela | Moore    | 43  | 1981-03-30   |
| David    | Williams | 60  | 1964-03-06   |

Our goal is to show you how to:

1. Import this data into Memgraph.
2. Use it for analysis.
3. Apply techniques you learned to your own data.

## Analyze and break down data

To analyze and break down data, first open the file and inspect its structure.

**Objective:** Learn how to structure data as a graph by identifying:
- **Key entities:**¬†What are the nodes in your data?
- **Connections:**¬†What relationships link these entities?

**Example:**¬†Treat the dummy data as a simplified version of users‚Äô complex
data. Identify entities like¬†`Person`and decide on potential relationships
(friendships, families, etc.).

The key question to ask yourself is: What do I want to achieve with my data, and
how should I model it to make that happen? 

For this guide, we‚Äôll focus on modeling friendships. However, always tailor your
graph to highlight the¬†value in your own data¬†by thinking about expected inputs
and outputs. When doing the same on your data, approach it with this question:
‚ÄúWhat are my key entities and how are they related?‚Äù

## Design your graph model

After analyzing and breaking down data, the next step is to design a preliminary
graph model using the dummy data. 

**Objective:** Transition from a flat file to a graph model by defining nodes
and relationships.

**Example:**
- **Nodes:**¬†`Person`¬†(with properties like name, surname, age, and date of
  birth).
- **Relationships:** `FRIENDS_WITH`, `CONNECTED_TO` or similar relationships
  representing the connection between different persons.

Moving on to transferring data into graph structure. As we mentioned earlier,
each¬†**entity**¬†will be represented as a¬†**node**. In our example, we‚Äôll call
these nodes¬†`Person` nodes. Based on the data we have, each `Person` node will
have the following properties: name, surname, age, and date of birth.

Now that we know how to map entities to nodes, the next step is deciding what to
do with this data. This decision will depend on the specific use case. 

For instance, let‚Äôs assume that `Person` 1 is considered a friend of `Person` 2 if
they share the same birthdate. Defining these kinds of relationships early on is
crucial because knowing your goals in advance makes it easier to set everything
up correctly from the start.

Now that we‚Äôve structured our data, it‚Äôs time to start working with Memgraph.

## Learn Cypher syntax with dummy data

To interact with Memgraph, it is important to learn the basics of [Cypher](/querying). 

**Objective**: Understand how to create nodes, relationships, and perform queries.

Memgraph uses the¬†**Cypher query language** to interact with the database. While
there is a bit of a learning curve, you‚Äôll find it helpful in the long run.
Cypher is easy to read and maintain, and it offers greater expressiveness
compared to traditional SQL.

<Cards>
  <Cards.Card
    title="Learn Cypher"
    href="https://playground.memgraph.com/"
  />
</Cards>

<h3 className="custom-header">Creating nodes</h3>

To create a node in Memgraph, we use
the¬†[`CREATE`](/querying/create-graph-objects#create) clause. For example, let‚Äôs
create a `Person` node:

```cypher
CREATE (:Person {name: 'John', surname: 'Doe', age: 30, date_of_birth: date('1990-01-01')});
```

Let‚Äôs break it down:
- The `()` syntax defines the node. `:Person`¬†is the¬†**label**¬†we assign to the node. In a graph, you‚Äôll have
  different types of entities, and labels help you distinguish between them.
- Inside the¬†`{}`¬†brackets, we define the¬†**properties**¬†of the node, which give
  more details about the entity. Properties are often used for analytics or
  filtering.
- **Cypher automatically handles data types**¬†for you, so there‚Äôs no need to
  specify them‚ÄîMemgraph takes care of that.


<h3 className="custom-header">Creating relationships</h3>

To [create a
relationship](/querying/create-graph-objects#creating-relationships) between
nodes, we use this syntax:

```cypher
MATCH (p1:Person {name: 'John'}), (p2:Person {name: 'Jane'})
CREATE (p1)-[:FRIENDS_WITH]->(p2);
```

Here‚Äôs what‚Äôs happening:

- We first¬†**match**¬†two existing nodes (John and Jane) using their properties.
- The¬†`CREATE`¬†statement establishes a relationship between them.
- The¬†`[:FRIENDS_WITH]->`¬†syntax defines the relationship. The¬†`[]`¬†brackets
  indicate a relationship in Cypher, and¬†`FRIENDS_WITH`¬†is the **type** of relationship.

Try creating nodes for John and Jane and then establish a relationship between
them.

<h3 className="custom-header">Querying nodes</h3>

To search for a specific node, you can use the¬†`MATCH`¬†and¬†`RETURN`¬†keywords:

```cypher
MATCH (p:Person) WHERE p.name = 'John' RETURN p;
```

This will return all nodes where the `name` property is 'John'. You can also query
by other properties:

```cypher
MATCH (p:Person) WHERE p.age > 30 RETURN p;
```

To return a specific property, such as the `name`, run the following query:

```cypher
MATCH (p:Person) WHERE p.age > 30
RETURN p.name;
```

Or if you want to know how many people named John exist in the database:

```cypher
MATCH (p:Person) WHERE p.name = 'John'
RETURN COUNT(p);
```

Cypher offers a lot of flexibility for [querying](/querying) your data. Try
different queries to get comfortable with all the possibilities.

<h3 className="custom-header">Advantages of graph relationships</h3>

A big advantage of using relationships between nodes is that you don‚Äôt need to
duplicate data. When you later search for relationships, Memgraph automatically
retrieves the connected nodes without needing to store redundant information.
This is a key difference from traditional SQL databases, where relationships
often require separate tables with duplicated data.

## Create nodes

The next step is to import the dummy CSV file into Memgraph using the¬†[`LOAD
CSV`](/querying/clauses/load-csv) clause and create `Person` nodes.

**Objective:** Detect data that needs to be cleaned or trasformed for proper
data import (e.g., convert strings to integers or dates).

In the dummy CSV file, the date of birth is stored as a string. Storing dates as
strings can be inefficient and hard to work with. This is where
Memgraph‚Äôs¬†[temporal types](/fundamentals/data-types#temporal-types)¬†come in. In
Cypher, you can transform a date string into a temporal object using
the¬†`date()`¬†function, which makes date comparisons easier and more efficient.

Let‚Äôs create a¬†`Person`¬†node with a temporal date of birth:

```cypher
CREATE (:Person {
    name: 'Jane',
    surname: 'Smith',
    age: 25,
    date_of_birth: date('1995-05-10')
});
```

If you run the following query:

```cypher
MATCH (p:Person) RETURN p;
```

You‚Äôll notice that the¬†`date_of_birth`¬†field is now an object with separate
fields for the year, month, and day, all stored as integers. This makes date
comparisons much simpler. For example, you can easily compare dates like this:

```cypher
MATCH (p:Person) WHERE p.date_of_birth > date('1990-01-01') RETURN p;
```

This query will return all `Person` nodes with a date of birth after January 1,
1990. You can also make more granular comparisons. If you‚Äôre interested in
finding all people born on the 10th day of any month, run the following query:

```cypher
MATCH (p:Person) WHERE p.date_of_birth.day = 10 RETURN p;
```

This is far more practical than working with strings, and it makes your Cypher
easier to read and maintain.

## Create relationships

The next step is to create relationships between the `Person` nodes based on
logical assumptions (e.g., people born in the same year could be considered
friends).

**Objective:** Learn how to model relationships by identifying
implicit or explicit connections.

**Example:**

```cypher
MATCH (p1:Person), (p2:Person) WHERE p1.date_of_birth = p2.date_of_birth
CREATE (p1)-[:FRIENDS_WITH]->(p2);
```

<h3 className="custom-header">Creating relationships between people with the same name in Memgraph</h3>

After successfully importing `Person` nodes and interacting with them, it's time
to create relationships between these nodes. In this example, we will model
friendships based on a simple rule: people are considered friends if they share
the same name.

For demonstration purposes, we‚Äôll focus on individuals named "John" and limit
the number of created relationships to 100,000 to ensure efficiency and clarity.
Here's how you can implement this logic in Memgraph using Cypher.

<h4 className="custom-header">Step 1: Create relationships between friends</h4>

The following query identifies all pairs of nodes labeled `Person` with the name
"John" and creates a `FRIENDS_WITH` relationship between them. To avoid
self-loops, the query ensures that a node cannot create a relationship with
itself (`p1 <> p2`). Additionally, a `LIMIT` is applied to restrict the total
number of relationships created to 100,000:

```cypher
MATCH (p1:Person {name: "John"}), (p2:Person {name: "John"})
WHERE p1 <> p2
WITH p1, p2
LIMIT 100000
CREATE (p1)-[:FRIENDS_WITH]->(p2);
```

Here‚Äôs more information:

1. **`MATCH` clause**:
    - Identifies all `Person` nodes with the name "John."
    - Creates combinations of pairs (`p1` and `p2`), ensuring that `p1` and `p2` are not the same person (`p1 <> p2`) to
      prevent self-loops.
2. **`WITH` clause**:
    - Passes the node pairs (`p1` and `p2`) to the next part of the query while
      applying the `LIMIT` constraint.
3. **`LIMIT` clause**:
    - Restricts the total number of relationships created to 100,000.
4. **`CREATE` clause**:
    - Establishes the `FRIENDS_WITH` relationship between the selected pairs.

<h4 className="custom-header">Step 2: Inspect the created relationships</h4>

To verify the created relationships and visualize the results, run the following
query:

```cypher
MATCH (n)-[r:FRIENDS_WITH]->(m)
RETURN n, r, m
LIMIT 10;
```

[ADD VISUAL]

Here‚Äôs more information:

1. **`MATCH` Clause**:
    - Retrieves all nodes (`n` and `m`) connected by a `[:FRIENDS_WITH]`
      relationship.
2. **`RETURN` Clause**:
    - Returns the starting node (`n`), the relationship (`r`), and the ending
      node (`m`) for inspection.
3. **`LIMIT 10`**:
    - Displays only the first 10 relationships for simplicity and quick
      validation.

By running these queries, you can

- Model friendships between individuals who share the same name in Memgraph.
- Limit the number of relationships created for efficiency.
- Visualize and inspect the newly created `[:FRIENDS_WITH]` relationships to
  confirm your data modeling logic.

This approach demonstrates how easy it is to build and explore relationships
within your graph database using Memgraph.

## **CHAPTER 8: Optimize with indexes**

**Task:**¬†Create indexes to speed up querying on frequently used properties.

**Objective:** Learn how indexes improve query performance, especially for
larger datasets.

**Example:**

```
cypher

CREATE INDEX ON :Person(name);
CREATE INDEX ON :Person(surname);
```

### Optimizing relationship creation with indices in Memgraph

Creating relationships in Memgraph is already fast compared to many other
technologies, but it can be further optimized using indices. 

### Initial performance without indices

In our earlier example, we modeled friendships between people with the same
name, specifically "John." The query we used was:

```
cypher

MATCH (p1:Person {name: "John"}), (p2:Person {name: "John"})
WHERE p1 <> p2
WITH p1, p2
LIMIT 100000
CREATE (p1)-[:FRIENDS_WITH]->(p2);
```

Executing this query without any indices took approximately **3.5 seconds**,
which is already fast for processing large datasets. However, we can make this
even faster by introducing indices.

### Step 1: Adding indices to improve performance

Memgraph supports two types of indices that can improve query performance:

- **Label index -** Speeds up searches based on the label of a node.
- **Property index -** Enhances performance when filtering nodes based on a
  specific property.

To create indices in Memgraph, use the `CREATE INDEX` syntax. Here‚Äôs how we can
optimize our query:

**Create an index on the `Person` label**:

```
cypher

CREATE INDEX ON :Person;
```

This ensures faster lookup of nodes labeled as `Person`.

**Create an index on the `name` property**:

```
cypher

CREATE INDEX ON :Person(name);
```

Since we filter by the `name` property (`name: "John"` or `name: "Anna"`), this
index will significantly speed up filtering.

---

### Step 2: Running the query after indexing

With the indices in place, we can rerun the query, this time focusing on people
named "Anna":

```
cypher

MATCH (p1:Person {name: "Anna"}), (p2:Person {name: "Anna"})
WHERE p1 <> p2
WITH p1, p2
LIMIT 100000
CREATE (p1)-[:FRIENDS_WITH]->(p2);
```

### Results and performance comparison

After adding indices, the same query that previously took **3.5 seconds** to
execute now runs in just **676 milliseconds**. That‚Äôs a **5x performance
improvement**!

Why indices matter?

Indices are a great tool in Memgraph, particularly when dealing with large
datasets and frequent queries. Here's why:

- **Faster node lookup -** Label indices make it quicker to find nodes of a
  specific type.
- **Efficient Filtering -** Property indices improve the speed of filtering
  operations, especially on frequently queried properties like `name`.

```
cypher

CREATE (:Person {name: 'Alice', surname: 'Wong', important_dates: [date('1992-01-01'), date('2020-05-15')]});

```

## **CHAPTER 9: Test and optimize queries**

**Task:**¬†Write and test queries on the dummy data to ensure performance and
structure.

**Objective:** Learn how to query efficiently and test if their re-modeled data
performs as expected in Memgraph.

**Example:**

```
cypher

MATCH (p:Person) WHERE p.age > 30 RETURN p;
MATCH (p:Person) WHERE p.date_of_birth = date('1990-01-01') RETURN p;
```

When working with large datasets in Memgraph, even simple queries can benefit
significantly from optimization. Let‚Äôs walk through an example where we retrieve
all people older than 35 years from the graph and apply an optimization using
indices to improve query performance.

Here‚Äôs the initial query we used to retrieve all people with an `age` of 35 or
older:

```
cypher

MATCH (p1:Person)
WHERE p1.age >= 35
RETURN p1;
```

Result:

- Number of nodes found: 792,500 people.
- Execution time: 14.68 seconds.

While this query is straightforward and correct, the execution time can be
optimized, especially when dealing with such a large dataset.

### Step 1: Optimization through indexing

Since the bottleneck in this query comes from filtering the `Person` nodes by
their `age` property, adding an index on the `age` property will allow Memgraph
to locate matching nodes more efficiently.

To create the index, run the following query:

```
cypher

CREATE INDEX ON :Person(age);
```

Why indexing helps?

- **Faster filtering -** Without an index, Memgraph must scan through all nodes
  labeled `Person` to evaluate the `age` property for each one. Adding an index
  allows Memgraph to directly access nodes with the desired `age` range.
- **Scalability -** Indexing ensures that the query performs better as the
  dataset grows, making it a critical optimization for large-scale graphs.

### Step 2: Re-running the query with the index

After creating the index on the `age` property, we re-ran the same query:

```
cypher

MATCH (p1:Person)
WHERE p1.age >= 35
RETURN p1;
```

Results:

- Execution time: 13.55 seconds.
- Improvement: Execution time was reduced by 8%, saving over 1.1 seconds on a
  query processing nearly 800,000 nodes.

<aside>
üí°

**PRO TIP**

- Optimization on simple queries matters.
- Indexing is a low-cost, high-impact optimization.
- Scalability benefits as your data size increases.
</aside>

## Next steps

You‚Äôve made it till the end of this data modeling how-to guide. Now go and see
how you can re-model your own dataset.

If you have any questions, comments, or are unsure how to proceed, join our
[Memgraph Discord community](https://discord.com/invite/memgraph) and we and
other users will be happy to help.